buildscript {
    repositories {
        jcenter()
    }
    
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'

group 'com.devbuild.api.scraps'
version '0.0.1'
mainClassName = "io.ktor.server.netty.EngineMain"

sourceCompatibility = 1.8
compileKotlin { kotlinOptions.jvmTarget = "1.8" }
compileTestKotlin { kotlinOptions.jvmTarget = "1.8" }

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://kotlin.bintray.com/ktor' }
    maven { url 'https://raw.githubusercontent.com/holowizor/repository/master'}
}

dependencies {
    def withoutJacksonModule = { exclude group: "com.fasterxml.jackson.module", module: "jackson-module-kotlin" }

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "ch.qos.logback:logback-classic:$logback_version"
    compile "io.ktor:ktor-server-core:$ktor_version"
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "io.ktor:ktor-server-host-common:$ktor_version"
    compile "io.ktor:ktor-jackson:$ktor_version", withoutJacksonModule

    compile "com.devbuild.commons:db-common:0.0.1"
    compile "com.devbuild.commons:auth-common:0.0.1", withoutJacksonModule

    testCompile "io.ktor:ktor-server-tests:$ktor_version"
}

task fatJar(type: Jar) {
    manifest {
        attributes "Implementation-Title": "Devbuild Scraps API",
                "Implementation-Version": version,
                "Main-Class": "io.ktor.server.netty.EngineMain"
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
